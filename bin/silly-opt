#! /usr/bin/env perl

#-----------------------------------------------------------------------------
# POD Format Documentation.  Read "perldoc perlpod" for an example.
# When done, check syntax with "podchecker".

=head1 NAME

silly-opt - Run mlir-opt against a silly-dialect file.

=head1 SYNOPSIS

silly-opt [--help] [--generic | --pretty] [--source myfile.mlir] [--out myout.mlir] [--verbose] [--pluginpath dir]

=head1 DESCRIPTION

Process a silly dialect mlir file (or stdin) using mlir-opt.  By default output goes to stdout.

Options:

=over 4

=item --pluginpath dir

Path to the plugin shared object (just the directory part: .../build/lib)

=item --output thefile.mlir

Use an output file instead of stdout.

=item --source thefile.mlir

Use an input file instead of stdin.

=item --generic

Run the asm printer in generic mode.

=item --pretty

Runs: cse, canonicalize, and symbol-dce

canonicalize may change semantics slightly (folds constants, etc.)
symbol-dce removes dead symbols â€” might surprise if someone expects full round-trip
cse (common subexpression elimination) just generally makes the output easier to read.

Strictly speaking this is more than just making it look pretty, but it does that too.

=back

=head1 SUPPORTED PLATFORMS

 Unix (Linux verified)

=head1 SUPPORT

 Send email to peeterjoot@pm.me

=head1 AUTHORS

 Peeter Joot

=cut

#-----------------------------------------------------------------------------

use strict ;
use warnings ;
use Getopt::Long ;
use Pod::Usage ;

# Suppress sourcing of users' .bashrc files in invoked shells
delete $ENV{'ENV'} ;
delete $ENV{'BASH_ENV'} ;

# Set STDOUT and STDERR to unbuffered
select STDERR ; $| = 1 ;
select STDOUT ; $| = 1 ;

my $myName = '' ;
my $generic;
my $pretty;
my $source;
my $out;
my $verbose = 0;
my $pluginpath;
($myName = $0) =~ s@.*[/\\]@@ ;

#Getopt::Long::Configure( 'pass_through' ) ;
GetOptions (
   'help'               => sub { pod2usage(-verbose => 2) ; },
   'generic!'	         => \$generic,
   'pretty!'	         => \$pretty,
   'source=s'           => \$source,
   'pluginpath=s'       => \$pluginpath,
   'out=s'              => \$out,
   'verbose!'           => \$verbose,
) or pod2usage(-verbose => 0) ;

my @prefix = <'/usr/local/llvm-*'>;
die unless ( scalar(@prefix) eq 1 );
my $prefix = $prefix[0];

my $MLIROPT = "$prefix/bin/mlir-opt";

my $opts = '';

# Print in generic mode (to see internal representation):
if ( $generic )
{
   $opts .= ' -mlir-print-op-generic';
}

# nice easy to read output:
if ( $pretty )
{
   $opts .= ' -canonicalize -cse -symbol-dce';
}

if ( !defined $source )
{
   $source = '-';
}

if ( !defined $out )
{
   $out = '-';
}

if (!defined $pluginpath)
{
   my $TOP = `git rev-parse --show-toplevel 2>/dev/null` ; chomp $TOP ;
   $pluginpath = "${TOP}/build/lib";
}
my $cmd = "${MLIROPT} --load-dialect-plugin=${pluginpath}/libSillyDialect.so ${opts} ${source} -o ${out}";
my $rc = mysystem( $cmd );

exit $rc;

sub mysystem
{
   my ($cmd) = @_;

   print "# $cmd\n" if ( $verbose );

   system( $cmd );
   my $rc = $? >> 8;

   return $rc;
}

# vim: et ts=3 sw=3
