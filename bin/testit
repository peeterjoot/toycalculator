#!/usr/bin/perl

#-----------------------------------------------------------------------------
# POD Format Documentation.  Read "perldoc perlpod" for an example.
# When done, check syntax with "podchecker".

=head1 NAME

testit - A little test driver.

=head1 SYNOPSIS

testit [--help] --no-fatal [--just tcname] [--optimize] [--no-debug]

=head1 DESCRIPTION

Options:

=over 4

=item --no-fatal

Warn, insted of die, on error.

=item --just testname

Run only testname.  Given full source test.silly, say, this is the test file stem test.

=item --no-debug

Do not pass -g to compiler.

=item --optimize

Pass -O 2 to compiler.

=back

=head1 SUPPORTED PLATFORMS

 Unix (Linux verified)

=head1 SUPPORT

 Send email to peeterjoot@pm.me

=head1 AUTHORS

 Peeter Joot

=cut

#-----------------------------------------------------------------------------

use strict ;
use warnings ;
use Getopt::Long ;
use Pod::Usage ;

# Suppress sourcing of users' .bashrc files in invoked shells
delete $ENV{'ENV'} ;
delete $ENV{'BASH_ENV'} ;

# Set STDOUT and STDERR to unbuffered
select STDERR ; $| = 1 ;
select STDOUT ; $| = 1 ;

my $myName = '' ;

($myName = $0) =~ s@.*[/\\]@@ ;
my $fatal = 1;
my $just;
my $optimize = 0;
my $debug = 1;
my $assembly = 1;

#Getopt::Long::Configure( 'pass_through' ) ;
GetOptions (
   'help'               => sub { pod2usage(-verbose => 2) ; },
   'fatal!'             => \$fatal,
   'just=s'             => \$just,
   'optimize!'          => \$optimize,
   'assembly!'          => \$assembly,
   'debug!'             => \$debug,
) or pod2usage(-verbose => 0) ;

# Validate/handle options, and everything else...

# normalize input:
if ( defined $just )
{
    $just =~ s/\.silly$//;
}

my $flags = '';

if ( $optimize )
{
    $flags .= ' -O 2';
}

if ( $debug )
{
    $flags .= ' -g';
}

system( qq(rm -rf out) );

my %tests = map { $_ => 1 } (qw(
0minus
addi
andop
array
bin
bool
boolop
boolr
call_with_param_referenced
converti
dcl
elif
elif2
elif3
elif4
elif5
elif6
empty
eqop
exit3
exitarrayelement
exitx
f
factorial
foo
function
function_add
function_foo_with_local
function_intret_intparam
function_intret_void
function_plist
function_plist_nocast
function_return
function_two
function_void_intparm
function_void_void
get
getbool
getboolfail
if
ifdcl
intarray
lessbug
lessbug2
lessbug3
lesseqop
lessop
longstring
neg
neqop
not
orop
printboollit
printlit
print_multiple
shortstring
shortstring2
shortstring3
simpleless
simplest
stringlit
test
types
unary
xorop
));

my %failuretests = map {$_ => 1} (qw(
error_redeclare
error_unassigned
error_undeclare
error_invalid_binary
error_invalid_unary
error_keyword_declare
error_keyword_declare2
));
#error_intarray_bad_constaccess

if ( defined $just )
{
    $tests{$just}++ unless ( defined $tests{$just} );
}

my %expectedRC = (
   'bool' => 1,
   'boolr' => 1,
   'exit3' => 3,
   'exitx' => 3,
   'exitarrayelement' => 42,
   'getboolfail' => '134', # abort
);

my %expectedInput = (
    'get' => '6 7 1 2 3 4 5',
    'getbool' => '0 1',
    'getboolfail' => '2',
);

my @warnings = ();

sub verbose_system
{
    my ($cmd) = @_;

    print "# $cmd\n";
    system( $cmd );

    return $? >> 8;
}

my $pwd = `pwd` ; chomp $pwd;
my @alltests = keys %tests;
push(@alltests, keys %failuretests);

foreach my $stem (sort @alltests)
{
    next if ( $stem =~ /^#/ );
    next if ( defined $just and $just ne $stem );

    print "##########################################################################\n";
    verbose_system( qq(cat $stem.silly) );

    print "##########################################################################\n";
    my $cmd = qq(../build/silly --output-directory out $stem.silly $flags --emit-llvm --emit-mlir);
    my $crc = verbose_system( $cmd );
    if ( defined $failuretests{$stem} )
    {
        if ($crc)
        {
            print "TEST: $stem.silly failed as expected: rc = $crc\n";
        }
        else
        {
            die "Expected failure running $stem.silly";
        }

        next;
    }

    verbose_system( qq(objdump -dr --no-show-raw-insn out/${stem}.o > out/${stem}.s) );

    if ( $assembly )
    {
        verbose_system( qq(cat out/${stem}.s) );
    }

    my $rc;
    if ( defined $expectedInput{${stem}} ) {
        $rc = verbose_system( qq(echo "$expectedInput{${stem}}" | ./out/${stem} > out/${stem}.out) );
    } else {
        $rc = verbose_system( qq(./out/${stem} > out/${stem}.out) );
    }
    verbose_system( qq(cat out/${stem}.out) );

    print "${stem}: RC = $rc\n\n\n\n\n";

    my $erc = $expectedRC{$stem};
    if ( !defined $erc )
    {
        $erc = 0;
    }

    complain( "stem: $rc != $erc" ) if ( $rc ne $erc );

    if ( -e "expected/$stem.out" )
    {
        my $crc = verbose_system( qq(diff -wup expected/${stem}.out out/${stem}.out) );
        complain( "ERROR: diff -wup expected/${stem}.out out/${stem}.out: $crc\n") if ( $crc );
    }
    else
    {
        my $msg = "ERROR: COMPARE FILE NOT FOUND: expected/$stem.out\n";
        push( @warnings, $msg );
    }

    # DI testing HACK for one issue, now fixed:
    if ( $stem eq 'f' )
    {
        # Could check a lot more, but start with this:
        my $cmd = qq(dwarfdump out/f.o | tee out/f.dd | grep -A3 DW_AT_linkage_name.*bar0 | tail -1);
        my $line = `$cmd` ; chomp $line;

        unless ( $line =~ /DW_AT_decl_line.*0x00000003/ )
        {
            complain( "ERROR: unexpected dwarfdump output for f.silly in out/f.dd.  Expected DW_AT_decl_line 0x00000003, found: '$line'" );
        }
    }
}

if ( scalar( @warnings ) )
{
    print "ERRORS:\n\n";

    foreach ( @warnings )
    {
        warn $_;
    }
}

exit 0;

sub complain
{
    my ($message) = @_;

    if ( $fatal )
    {
        die $message;
    }
    else
    {
        push( @warnings, $message );
    }
}

# vim: et ts=4 sw=4
