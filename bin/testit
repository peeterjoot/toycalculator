#!/usr/bin/perl

#-----------------------------------------------------------------------------
# POD Format Documentation.  Read "perldoc perlpod" for an example.
# When done, check syntax with "podchecker".

=head1 NAME

testit - A little test driver.

=head1 SYNOPSIS

testit [--help] --no-fatal [--just tcname] [--optimize] [--no-debug]

=head1 DESCRIPTION

Options:

=over 4

=item --no-fatal

Warn, insted of die, on error.

=item --just testname

Run only testname.  Given full source test.toy, say, this is the test file stem test.

=item --no-debug

Do not pass -g to compiler.

=item --optimize

Pass -O 2 to compiler.

=back

=head1 SUPPORTED PLATFORMS

 Unix (Linux verified)

=head1 SUPPORT

 Send email to peeterjoot@pm.me

=head1 AUTHORS

 Peeter Joot

=cut

#-----------------------------------------------------------------------------

use strict ;
use warnings ;
use Getopt::Long ;
use Pod::Usage ;

# Suppress sourcing of users' .bashrc files in invoked shells
delete $ENV{'ENV'} ;
delete $ENV{'BASH_ENV'} ;

# Set STDOUT and STDERR to unbuffered
select STDERR ; $| = 1 ;
select STDOUT ; $| = 1 ;

my $myName = '' ;

($myName = $0) =~ s@.*[/\\]@@ ;
my $fatal = 1;
my $just;
my $optimize = 0;
my $debug = 1;
my $assembly = 1;

#Getopt::Long::Configure( 'pass_through' ) ;
GetOptions (
   'help'               => sub { pod2usage(-verbose => 2) ; },
   'fatal!'             => \$fatal,
   'just=s'             => \$just,
   'optimize!'          => \$optimize,
   'assembly!'          => \$assembly,
   'debug!'             => \$debug,
) or pod2usage(-verbose => 0) ;

# Validate/handle options, and everything else...

my $flags = '';

if ( $optimize )
{
    $flags .= ' -O 2';
}

if ( $debug )
{
    $flags .= ' -g';
}

system( qq(rm -rf out) );

my %tests = map { $_ => 1 } (qw(
addi
andop
array
bin
bool
boolop
shortstring
converti
dcl
empty
eqop
exit3
exitx
foo
lessbug
lessbug2
lessbug3
lesseqop
lessop
neqop
orop
simpleless
simplest
stringlit
test
types
unary
xorop
));

if ( defined $just )
{
    $tests{$just}++ unless ( defined $tests{$just} );
}

my %expectedRC = (
   'bool' => 1,
   'exit3' => 3,
   'exitx' => 3,
);

my @warnings = ();

sub verbose_system
{
    my ($cmd) = @_;

    print "# $cmd\n";
    system( $cmd );

    return $? >> 8;
}

my $pwd = `pwd` ; chomp $pwd;
foreach my $stem (sort keys %tests)
{
    next if ( defined $just and $just ne $stem );

    print "##########################################################################\n";
    verbose_system( qq(cat $stem.toy) );

    print "##########################################################################\n";
    my $cmd = qq(../build/toycalculator --output-directory out $stem.toy $flags --emit-llvm --emit-mlir);
    verbose_system( $cmd );

    verbose_system( qq(objdump -dr --no-show-raw-insn out/${stem}.o > out/${stem}.s) );

    if ( $assembly )
    {
        verbose_system( qq(cat out/${stem}.s) );
    }

    my $rc = verbose_system( qq(./out/${stem} > out/${stem}.out) );
    verbose_system( qq(cat out/${stem}.out) );

    print "${stem}: RC = $rc\n\n\n\n\n";

    my $erc = $expectedRC{$stem};
    if ( !defined $erc )
    {
        $erc = 0;
    }

    complain( "stem: $rc != $erc" ) if ( $rc ne $erc );

    if ( -e "expected/$stem.out" )
    {
        my $crc = verbose_system( qq(diff -wup expected/${stem}.out out/${stem}.out) );
        complain( "ERROR: diff -wup expected/${stem}.out out/${stem}.out: $crc\n") if ( $crc );
    }
    else
    {
        my $msg = "ERROR: COMPARE FILE NOT FOUND: expected/$stem.out\n";
        push( @warnings, $msg );
    }
}

if ( scalar( @warnings ) )
{
    print "ERRORS:\n\n";

    foreach ( @warnings )
    {
        warn $_;
    }
}

exit 0;

sub complain
{
    my ($message) = @_;

    if ( $fatal )
    {
        die $message;
    }
    else
    {
        push( @warnings, $message );
    }
}

# vim: et ts=4 sw=4
